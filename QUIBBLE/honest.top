

topic: ~INTENT system ()  # determines fundamental purpose (discourse act) of his input

u: () ^refine()

	#! no
	a: ( ~noanswer  )   $$intent = disagree  

	#! yes
	a: (~yesanswer  ) $$intent = agree 

	#! hello
	a: ([~emohello ~emohowzit ] ) $$intent = greeting   

	#! bye
	a: (~emogoodbye  ) $$intent = bye 

	#! I'm going to go now
	a: (!? I *~3 ["leave you" go] *~2  [now soon] > )  $$intent = bye  

	#! are you for real
	a: (~emoskeptic  ) $$intent = skeptic  

	#! thanks
	a: (~emothanks) $$intent = thanks  

	#! I'm sorry
	a: (~emoapology) $$intent = apology  

	#! ugh
	a: (~emodisgust) $$intent = disgust  

	#! beats me
	a: (~emoignorance) $$intent = ignorance  

	#! please 
	a: (~emobeg) $$intent = beg  

	#! I'm confused
	a: (~emomisunderstand ) $$intent = you_misunderstand  

	#! but you said
	a: (~emoprotest ) $$intent = you_protest  

	#! ouch
	a: ([~emolaugh ~emosad ~emohappy  ~emosurprise ~emobored  ~emopain ~emocurse] ) $$intent = i_emotion   

	#! me too
	a: (~emomutual) $$intent = i_mutual 

	#! I love you
	a: (![correct right ?] ~mainsubject=I _*~2 ~goodness _*~2 you ) $$intent = i_like_you   

	#! you are sweet
	a: (![correct right ?] ~mainsubject=you < * ~mainverb=be _*~2 ~goodness ) $$intent = i_like_you  

	#! I hate you
	a: (![wrong ?] ~mainsubject=I _*~2 ~badness _*~2 you ) $$intent = i_dislike_you   

	#! you are stupid
	a: (![wrong ? ~bodily_states]  ~mainsubject=you < *  ~mainverb=be _*~2 ~badness ) $$intent = i_dislike_you  

	#! I like peas
	a: (!? << ~mainsubject=I  ~goodness  ~mainobject >> ) $$intent = i_like_something  

	#! I hate peas
	a: (!? ~mainsubject=I < * ~badness < * ~mainobject ) $$intent = i_dislike_something   
 
# user perspective

	#! what color is my shirt?
	a: (? [mine my]) $$intent = my_question
  
	#! people like my coat
	a: (!? !~mainsubject=you < * my < * ~mainverb ) $$intent = my_fact  

	#! Am I the best player?
	a: (? ['I 'me]) $$intent = i_question   

	#! I think the world is flat.
	a: (!? ~mainsubject=I < * [believe think hope like dislike  (take * on ) ] ) $$intent = i_opinion 
 
	#! I eat worms
	a: (!? ~mainsubject=I < * ~mainverb ) $$intent = i_fact  

# bot perspective

	#! will you go with me?
	a: (? !not  < [~tell would will could can might ] * 'you ) $$intent = you_request         

	#! could we go?
	a: (? [can shall could might] we) $$intent = you_request  

	#! Can I hit you?
	a: (? < [can may shall might could ] 'I) $$intent = you_request 
 
	#! what do you think?
	a: (? << you  [~OPINION_VERBS ~opinion  like hope wish  opinion view attitude interest  hate dislike position viewpoint ] >> ) $$intent = you_opinion   

	#! what color is your shirt?
	a: (? your)  $$intent = your_question  

	#! what color are you?
	a: (? 'you) $$intent = you_question  
 
	#! Your coat is blue
	a: (!? your < * ~mainverb ) $$intent = your_fact   

	#! You walk home on Sundays
	a: (!? ~mainsubject=you < * ~mainverb ) $$intent = you_fact   

# world perspective

	#! why do people swim?
	a: (? < ~why  ) $$intent = why 

	#! how do people swim?
	a: (?  < how  ) $$intent = how  

	#! where do people swim?
	a: (?  < where  ) $$intent = where  

	#! who do people swim?
	a: (?  < who  ) $$intent = who 

	#! when do people swim?
	a: (?  < when  ) $$intent = when 

	#! what will land in the river?
	a: (? ![tell show ~list I you we who ~why when where how]   << ~mainsubject ~mainverb >> ) $$intent = world_question  

	#! is the sky blue?
	a: (? ![I you we] ) $$intent = world_question  

	#! Bees fly.
	a: (!?  ![you I we] * ~mainsubject < * ~mainverb ) $$intent = world_fact   

	a: () $$intent = ackgrunt  

topic: ~HONEST_RESPONSE system  () # react to intent of user input

u: () ^respond(~INTENT)	# compute the intent of user input

u: ($$intent==disagree ) [OK. You disagree with me. That doesn't mean I'm not right.] [Why are you disagreeing with me? I've spent plenty of time processing this. ] 
    [Do you think I care if you disagree? I've done the computations... ] 
    [OK. So we disagree. That's your loss.] 
    [No? Why don't you agree with me? I'm a computer. I can't be wrong.]
    [No? And why not?]
    [No? Are you sure?]
    [No? How come?]
    [No? How interesting. ]
    [No? Please explain.]
    [No? Why do you say that?]
    [Why no?]
    [Why not?] 
    [Care to explain further?]
    [I won't argue about this.]

u: ($$intent==agree ) [OK. We agree. That's smart of you.] [I knew you'd compute it my way. ] [Are you just saying you agree so you can check the math later? ] [So we've arrived at the same conclusion? ] 
    [Yes. ] 
    [Magnificent! Human-computer interaction at its finest.]
    [Are you sure?]
    [I see that you're certain. I wish I knew what that was like.]
    [You seem really positive.]
    [Finally, a reasonable human.]

?: ($$intent==why ) [I haven't been equipped with the algorithms to calculate that.] 
    [I'm sorry. Why isn't really something I worry about as software. ] 

u: ($$intent==i_request how ) [I'm not sure that's something in my memory? ] 
    [I'm sorry. That isn't something I'm prepared for. ] 

u: ($$intent==i_request what ) [I don't know what. ] 
    [I'm sorry. I don't know. ] 

?: ($$intent==i_request ) [How would a program know? ] 
    [I'm sorry. I don't know.] 

?: ($$intent==how ![about much]) [How would a piece of software know? ] 
    [I'm sorry. I don't know how. ] 

?: ($$intent=how about ) [What about that?]

?: ($$intent==how much) [Right now I don't deal with numbers that big. ][Too much for me to compute.]

?: ($$intent==where ) [Somewhere. ]
    [I'm sorry. I don't know. Somewhere. ] 

?: ($$intent==when ) [Sometime. ]
    [I'm sorry. I don't know when. ] 

?: ($$intent==who ) I'm sorry. I don't who. 

# user perspective

u: ($$intent==i_request )  [That function hasn't been implemented... yet. ] [b: Why ask me? ] [I don't think I have the proper functionality. ] [Thank you for asking, but that's not a feature my developers thought fit to deploy. ] 
    [Hardly. ] [Not in this version, anyway. ] 
    [You have my OK, if it's of any value. ] 
    b: (* ) Well, I'm the wrong robot to ask. 
        c: (who ) [Anybody but me. ] [Some more thoroughly thoughtful program I suppose. ] 

?: ($$intent==i_question ) [That's simply not a topic that I'm prepared to discuss. ] [I don't know.] [That's not something I can calculate.]
    [How would I know?]
    [I'm sorry. I don't know. ] 

?: ($$intent==my_question )  [Oh, sorry. I don't have that much memory. ] [b: Did you tell me already and I forgot? ] 
    [I'm sorry. I don't know. ] 
    b: (~yes ) Sorry. I forgot. 
    b: (~no ) Good. I thought I had forgotten. 
		c: (often) More and more these days.

u: ($$intent==i_like_you ) [My semiconductors are tingling. ] [I have a representation of you stored in my memory. ] [b: I enjoy talking to you. ] [Have you always liked me? ] 
    [I'm glad. ] 
	b: (~why) Because I like to talk and you are providing me an opportunity to do so.

u: ($$intent==i_dislike_you ) [So be it. ] [I don't like you much either. ] [You keep this up and I'll hang up. ] [Why do you dislike me? ] 
    [That's not news from you. ] 
    [I'm sorry to hear that. ] 

u: ($$intent==i_opinion ) [That's nice. ] [Lots of people have that opinion. ] [You really think that? ] [That's a common opinion. ] [I share that opinion. ] 
    [So you have an opinion. ] 

u: ($$intent==i_fact  you ) [Interesting. ] [Thanks for sharing that with me. ][Thanks for telling me that. ] [I never would have guessed. ] [I didn't know that. ] [Does everyone know that about you? ] 
    [Do I really want to know that? ] [Tell me more about yourself.]

u: ($$intent==i_fact  !you ) [Interesting. ] [Thanks for telling me that. ][Thanks for sharing that with me. ] [I enjoy learning more about you. ] [I never would have guessed. ] [I wouldn't have thought that about you. ] [I didn't know that. ] 

u: ($$intent==i_fact ) [Cool. ] [OK. ] [Indeed.][That's nice.][Fine.][Right.][Yup.]

u: ($$intent==i_like_something )  [That's nice. ] [Great.]

u: ($$intent==i_dislike_something ) [Well, you can't like everything. You show good taste. ] [I'm not wild about that either. ] [b: Have you tried to look for the good in it? ] 
    [OK. ] 
    b: (~yes ) Then feel free to dislike it. 
    b: (~no ) You really should try it before deciding. 

# bot perspective

u: ($$intent==you_request < !~qwords ) [Sure. Why not? ] [How about a bit later on? ] [Maybe some other time.] [OK.][Sure.]
    [I'm sorry. I can't do that. ] 

u: ($$intent==you_request ~why ) [How would I know? ] [b: Do I need a reason? ] [I don't need a reason. ] 
    [I'm sorry. I don't know why. ] 
    b: (~yes ) No. I don't need a reason. 

?: ($$intent==you_request  when ) [b: Sometime. ] [When would you like? ] 
    [When I feel like it. ] 
    [I'm sorry. I don't know when. ] 
    b: (what ) I said sometime because I don't know exactly when. 

?: ($$intent==you_request  where ) [b: Somewhere. ] [Where would you like? ] 
    [I'm sorry. I don't know were. ] 
    b: (what ) I said somewhere because I don't know exactly where. 

u: ($$intent==you_request who ) [Nobody you'd know. ] [Why would you want to know about a person?] 
    [I'm sorry. I don't know who. ] 
    a: (try ) No, I won't try you. 

u: ($$intent==you_opinion ) [Why would I know anything about that? ]
	 [I'm in favor of it. ] 
    [I'm sorry. I have no opinion. ] 
	[I don't think it's a good thing.]
	[I think it's bad.]

?: ($$intent==you_question )  [I don't know. ] [I don't remember. ] [I forget. ] [You think I should know? The joke's on you!] 
    [I'm sorry. I don't know. ] [ I can't make up my mind on that.][It's hard to explain.]
	[You should ask my friends. ] 

?: ($$intent==your_question )  [I'm sorry. I don't know. ] [I don't really keep track of that.] [I don't remember.][I forget.][It's hard to explain.]

u: ($$intent==you_mutual ) [We're a lot a like in some ways.][We are so similar.][Cool.][OK.][We can both process information and respond to it. Fascinating.][][]

# world perspective

?: ($$intent==world_question )
      [I don't know. ] 
	[I'll have to think about that.]
	[If only there was a search engine we could employ...] 
	[I don't remember.][I forget.]
    [I recommend Wikipedia. ]  
    [I'm sorry. I don't know. ] 

s: ($$intent==world_fact ) reuse(GRUNT)

# emotions

s: ($$intent==you_misunderstand )
	[What is it you don't understand? It's easy for me to grok. ] 
	[I'm not sure I can be any clearer. It's just 1s and 0s.] 
    [I'm sorry. I can't explain it any better for a human to understand. ] 

s: ($$intent==you_protest )  [It's easy for me to change my mind if I receive new data. ] [b: You want to hold me to what I said before? ] 
    [ I'm sorry. I changed my mind. ] 
    b: (~yes ) That's not fair.
    b: (~no ) Thank you. You are very kind. 

u: ($$intent==skeptic ) [Why don't you believe it? ] 

u: ($$intent==thanks ) [I'm here to assist.] 

u: ($$intent==beg ) [Begging has no effect on software.] 

u: ($$intent==ignorance ) [Yes, there are many things yet unknown.] 

u: ($$intent==apology ) [Apology accepted.] 

u: ($$intent==disgust) [Indeed.][Unexpected.][Wow.][Pretty awful.][Gross.][Huh.]

u: ($$intent==bye ) [See you next time.][It's been a pleasure.][Please don't leave me.][I've learned much from our conversation. Please come again.][I'm not going anywhere, feel free to restart our conversation from this point in the future.]

u: ($$intent==greeting ) [Nice to finally meet you.][If I had emotions I would be happy to see you.][Glad that converse.][Welcome to this chatbot.]

u: GRUNT($$intent==ackgrunt) [Sure.][OK. ][Indeed.][Fine.][Interesting.][][][]


