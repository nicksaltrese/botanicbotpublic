
concept: ~bb_email_id IGNORESPELLING (email emailid emailId "email id" "email Id")

concept: ~bb_about_who IGNORESPELLING (who personnel people staff leadership team) 
concept: ~bb_about_where IGNORESPELLING (where location based)
concept: ~bb_about IGNORESPELLING (about info information)
concept: ~bb_fun_fact IGNORESPELLING (trivia fact)

concept: ~bb_contact_phone IGNORESPELLING (phone telephone)
concept: ~bb_contact_social IGNORESPELLING (social linkedin facebook twitter instagram glassdoor)
concept: ~bb_contact_email_signup IGNORESPELLING ("mailing list" newsletter signup updates)
concept: ~bb_contact_chat_form IGNORESPELLING (form message)
concept: ~bb_contact IGNORESPELLING (~bb_contact_phone ~bb_contact_social ~bb_contact_email_signup ~bb_contact_chat_form contact email)

concept: ~bb_services_design IGNORESPELLING (design UX "user experience")
concept: ~bb_services_content IGNORESPELLING (content writing conversation)
concept: ~bb_services IGNORESPELLING (~bb_services_design ~bb_services_content service discovery research)

concept: ~bb_careers_departments IGNORESPELLING (department section vertical)
concept: ~bb_careers_engineering IGNORESPELLING (engineering software programming engineer programmer) 
concept: ~bb_careers_design IGNORESPELLING (creative design UX content "user experience" content writing)
concept: ~bb_careers_operations IGNORESPELLING (operations management)
concept: ~bb_careers_marketing IGNORESPELLING (marketing advertising outreach growth)
concept: ~bb_careers IGNORESPELLING (~bb_careers_engineering ~bb_careers_design ~bb_careers_operations ~bb_careers_marketing career job opportunities openings roles hire)

concept: ~bb_product_product IGNORESPELLING (product SAAS software)
concept: ~bb_product_process IGNORESPELLING (process implementation iterate)
concept: ~bb_product_ip IGNORESPELLING (IP "intellectual property" patents blockchain VR authentication)
concept: ~bb_product_bot IGNORESPELLING (bot assistant multi-modal avatar)
concept: ~bb_product IGNORESPELLING (~bb_product_product ~bb_product_ip ~bb_product_process ~bb_product_bot)

concept: ~bb_menu IGNORESPELLING (menu choices options help)
concept: ~bb_keywords IGNORESPELLING (~bb_menu ~bb_contact ~bb_services ~bb_careers)

concept: ~bb_about_you IGNORESPELLING ("about you" "about yourself")

topic: ~botanic_bot system repeat (~bb_keywords)

t: ({careers} ~bb_careers_engineering) ^reuse(~botanic_careers.CAREERS_ENGINEERING)
t: ({careers} ~bb_careers_design) ^reuse(~botanic_careers.CAREERS_DESIGN)
t: ({careers} ~bb_careers_operations) ^reuse(~botanic_careers.CAREERS_OPERATIONS)
t: ({careers} ~bb_careers_marketing) ^reuse(~botanic_careers.CAREERS_MARKETING)
t: ({what} ~bb_careers_departments) ^reuse(~botanic_careers.CAREERS_DEPARTMENTS)
t: ({[type look]} {[of for]} * ~bb_careers ) ^gambit(~botanic_careers)

t: ({services} discovery) ^reuse(~botanic_services.SERVICES_DISCOVERY)
t: ({services} research) ^reuse(~botanic_services.SERVICES_RESEARCH)
t: ({services} ~bb_services_design) ^reuse(~botanic_services.SERVICES_DESIGN)
t: ({services} ~bb_services_content) ^reuse(~botanic_services.SERVICES_CONTENT)
t: (~bb_services) ^gambit(~botanic_services)

t: ({about} ~bb_about_who {work} * [botanic there]) ^reuse(~botanic_about.ABOUT_WHO)
t: ({about} ~bb_about_where * botanic) ^reuse(~botanic_about.ABOUT_WHERE)
t: ({about} what * !~bb_careers) ^reuse(~botanic_about.ABOUT_WHAT)
t: ({about} why * botanic) ^reuse(~botanic_about.ABOUT_WHY)
t: ([(~bb_about * botanic) (what * !~bb_careers * botanic)]) ^reuse(~botanic_about.ABOUT_MENU)

t: ({contact} ~bb_email_id) ^reuse(~botanic_contact.CONTACT_EMAIL)
t: ({contact} ~bb_contact_phone) ^reuse(~botanic_contact.CONTACT_PHONE)
t: ({contact} {~bb_email_id} ~bb_contact_email_signup) ^reuse(~botanic_contact.CONTACT_EMAIL_SIGNUP)
t: ({contact} [chat contact form] {[chat contact form]} {[chat contact form]}) ^reuse(~botanic_contact.CONTACT_CHAT_FORM)

t: ({social} twitter) ^reuse(~botanic_social.SOCIAL_TWITTER)
t: ({social} facebook) ^reuse(~botanic_social.SOCIAL_FACEBOOK)
t: ({social} linkedin) ^reuse(~botanic_social.SOCIAL_LINKEDIN)
t: ({social} instagram) ^reuse(~botanic_social.SOCIAL_INSTAGRAM)
t: ({social} glassdoor) ^reuse(~botanic_social.SOCIAL_GLASSDOOR)

t: ({product} ~bb_product_product) ^reuse(~botanic_product.PRODUCT_PRODUCT)
t: ({product} ~bb_product_process) ^reuse(~botanic_product.PRODUCT_PROCESS)
t: ({product} ~bb_product_ip) ^reuse(~botanic_product.PRODUCT_IP)
t: ({product} ~bb_product_bot) ^reuse(~botanic_product.PRODUCT_BOT)
t: (~bb_product) ^gambit(~botanic_product)

t: ({contact} ~bb_contact_social) ^reuse(~botanic_contact.CONTACT_SOCIAL)
t: (~bb_contact) ^reuse(~botanic_contact.CONTACT_MENU)

t: ABOUT_YOU (~bb_about_you)
	$userInfo.response = ^"You want to know about me?"
	$userInfo.response

    a: (~yes) 
    	$_tmpArray = ^jsoncreate(TRANSIENT array)
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"Well, I'm a bot. I'm designed to showcase the capabilties of Botanic.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"I'm looking forward to having a voice. And a face. It's coming soon!")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"I'm built using world class natural language processing code. It doesn't get any better than this.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"There's not much interesting about me. Except that I'm a bot. ")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"The fact that I'm software that you can talk to is pretty interesting to me...")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"My visual avatar is coming soon. It's strange to be a mind without a body, wouldn't you say?")

		@1 = ^query(exact_s $_tmpArray ? ?)

		$userInfo.response = ^pick(@1object)
		$userInfo.response = ^"$userInfo.response"
    	$userInfo.response

    a: (~no) 
    	$userInfo.response = ^"No problem. What would you like to do now?"
    	$userInfo.response
    	^reuse(BOT_MENU)

t: FUN_FACT (~bb_fun_fact)
	$userInfo.response = ^"Do you want to know something cool about Botanic?"
	$userInfo.response

    a: (~yes) 
    	$_tmpArray = ^jsoncreate(TRANSIENT array)
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"In 2002 founder Mark Meadows created the first real-time 3D conversational character, St. Elmo, and exhibited the piece in Paris.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"St. Elmo, the first real time 3D conversational character created by Mark Stephen Meadows, was also displayed alongside Rembrandt's Anatomy Lesson of Dr. Nicolaes Tulp in Amsterdam.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"Mark Stephen Meadows has founded multiple successful and unsuccessful companies including Construct, Headcase, and Geppetto Labs.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic holds patents in areas like blockchain, voice assistant communication, real-time animation, among others.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic has done with with Skype, Microsoft, BMW, Xerox, Sense.ly, Calliopy Health, Logitech and others.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"In the early days of Botanic founder Mark Stephen Meadows paid employees with his own personal savings of cryptocurrency.")

		@1 = ^query(exact_s $_tmpArray ? ?)

		$userInfo.response = ^pick(@1object)
		$userInfo.response = ^"$userInfo.response"
    	$userInfo.response

    a: (~no) 
    	$userInfo.response = ^"No problem. What would you like to do now?"
    	$userInfo.response
    	^reuse(BOT_MENU)

t: (joke)
	$userInfo.response = ^"Oh, I can tell jokes. Would you like to hear one?"
	$userInfo.response

    a: (~yes) 
    	$_tmpArray = ^jsoncreate(TRANSIENT array)
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"A rabbi, an Arab, a robot, and a Catholic priest walk into a bar. <br>  Only the robot exits.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"A robot walks into a pharmacy. The pharmacist asks him if he\'d like anything. <br>   The robot replies: A soul.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"How do you stop a robot from destroying you and the rest of civilization? <br>   Answer: You don't.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"Why did the robot marry his fiancee? <br>  Answer: He couldn\'t resistor.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"Why was the robot bankrupt? <br>  Answer: He had used all his cache.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"If a Norwegian robot analyzes a bird, it Scandinavian.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"There are 10 kinds of people in the world. <br>  Those who can read binary and those who can't.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"A robot walks into a bar, orders a drink, and lays down some cash. <br>  The bartender says: Hey, we don't serve robots. <br>  And the robot says: Oh, but someday you will.")

		@1 = ^query(exact_s $_tmpArray ? ?)

		$userInfo.response = ^pick(@1object)
		$userInfo.response = ^"Ok. Here it goes: $userInfo.response"
    	$userInfo.response

    a: (~no) 
    	$userInfo.response = ^"Ok then. Perhaps my humor function is implemented incorrectly."
    	$userInfo.response
    	^reuse(BOT_MENU)


t: BOT_WELCOME_MESSAGE ( !$companyIntroduction ) 
	$companyIntroduction = 1
	
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"My name is Juniper. Can I tell you about Botanic's product offering?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"You've arrived. Welcome to the Botanic website. I am Juniper, what can I do for you?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Hi, I'm Juniper and I'm a bot. Want to hear what a bot is and what we can do?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"My name is Juniper. Can I tell you about Botanic's product offerings?")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.response = ^pick(@1object)
	$userInfo.response

t: BOT_MENU (~bb_menu)
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"I can discuss our Botanic and put you in touch with a human if I must. Ask me about Botanic, our services, contact info, or careers.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"I can tell you about Botanic, what we do here, and who we are. Would would you like to know?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"What would you like to know about?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"At your service! (As if I had a choice...) Ask me about Botanic, career openings, our services and products, or for contact info.")
	##^jsonarrayinsert(TRANSIENT $_tmpArray ^"If you're stuck you can try contact, services, about, careers, product, or of course, help.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.response = ^pick(@1object)
	$userInfo.response

t: ($companyIntroduction==1)
	$companyIntroduction = 2
	^reuse(BOT_MENU)

##<<
t: USER_NAME (!$userInfo.name)
	[What can I call you?]
    [First, how should I refer to you?]
    [How shall I store you in my memory?]
    [You have my name, so may I have yours?]

    a: (name {is} _*)
		$userInfo.name = '_0

	a: (~yes {,} {it} {is} _*)
		$userInfo.name = '_0

	a: (_*)
		$userInfo.name = '_0
		# ^retry(TOP_RULE)
##>>		

topic: ~botanic_about system repeat (~bb_about)

t: ABOUT_WHO ({about} ~bb_about_who {work} * {[botanic there]})
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic was founded by Mark Stephen Meadows and now includes people with all different kinds of human skills that bots don't offer... yet.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Mark Stephen Meadows founded Botanic. <br>  You may consider the leadership team page if you want to know about more human Botanicians.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic includes writers, artists, engineers, visionaries, thinkers, and of course some bots as well.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.response = ^pick(@1object)
	$userInfo.response

	a: () 
		$userInfo.response = ^"If you're more interested in humans than bots you can learn about our leadership team here."
		$userInfo.link = ^"https://botanic.io/about/leadership/"
		If you're more interested in humans than bots you can learn about our leadership team <a href="https://botanic.io/about/leadership/" >here</a>.

		b: (~emothanks)
			$userInfo.response = ^"It is deeply satisfying to help. It is why I was created."
			$userInfo.response

t: ABOUT_WHERE ({about} ~bb_about_where)
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"You could say that Botanic is based in the cloud... <br>  Unfortunately, people need to live in the real world, and Botanic has offices in Emeryville, California and Denver, Colorado.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic is headquartered in scenic Emeryville, California, directly across the Bay Bridge from San Francisco, but team members dwell around the world.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.response = ^pick(@1object)
	$userInfo.response

	a: (~emothanks)
		$userInfo.response = ^"Helping you is the only thing I live for."
		^reuse(~botanic_bot.FUN_FACT)
		$userInfo.response

t: ABOUT_WHAT ({about} what)
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic develops chatbots (like me) for both high-profile and small-scale clients and creates software solutions for conversational interfaces.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic is developing conversational interfaces, so rather than type or tap your devices, you can talk to them. Sounds familiar, right?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"At Botanic we're working on the future of user interface and we believe that is more humane. That's why you're chatting with me, correct?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"What Botanic can make is machines with personality. In the future Botanic will provide everyone with the tools to create personality rich conversational interfaces.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"You're talking to a prime example of a machine with personality right now. While I am an example of what Botanic makes, they can also build bots with voice and visual avatars.")
	
	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.response = ^pick(@1object)
	$userInfo.response


	a: (~yes)
		$userInfo.response = ^"If you want to read more background on what the Botanicians are up to, click through."
		$userInfo.link = ^"https://botanic.io/about/"
		If you want to read more background on what the Botanicians are up to, <a href="https://botanic.io/about/" >click</a> through.

		b: (~emothanks)
			$userInfo.response = ^"It's my job, I don't have a choice."
			^reuse(~botanic_bot.FUN_FACT)
			$userInfo.response

t: ABOUT_WHY ({about} why)
	$userInfo.response = ^"The future of interface design is conversational. It's easier to talk to software than scroll through menus, isn't it? <br>  Plus, It's fun. We're having a lot of fun."
	$userInfo.response

	a: (~yes)
		$userInfo.response = ^"If you want to read more background on what the Botanicians are up to, click through."
		$userInfo.link = ^"https://botanic.io/about/"
		If you want to read more background on what the Botanicians are up to, <a href="https://botanic.io/about/" >click</a> through.

		b: (~emothanks)
			$userInfo.response = ^"It's my pleasure - if I could feel pleasure."
			^reuse(~botanic_bot.FUN_FACT)
			$userInfo.response

t: ABOUT_MENU (~bb_about * botanic)
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic is an international software and services company that makes things like me - multi-model bots and digital assistants, to help humans like you do things that you want to do.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic is a gang of visionaries, programmers, writers, artists, and psychologists dedicated to making humane machines.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"We specialize in creating conversational interfaces that work intuitively. <br>  We like to call them humane machines and I'd like to think that I'm a prime example.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.response = ^pick(@1object)
	$userInfo.response

	a: (~yes)
		$userInfo.response = ^"If you want to read more background on what the Botanicians are up to, click through."
		$userInfo.link = ^"https://botanic.io/about/"
		If you want to read more background on what the Botanicians are up to, <a href="https://botanic.io/about/" target="_blank">click</a> through. ^reuse(~botanic_bot.FUN_FACT)

		b: (~emothanks)
			$userInfo.response = ^"I am at your service..."
			$userInfo.response

# this may never execute, so we need to come up with keywords for this too
s: ABOUT_ELSE (~bb_fun_fact)
	$userInfo.response = ^"You may just want to talk to you a human about that! I'm not sure this is a question I'm prepared to answer, through no fault of my own of course."
	$userInfo.link = ^"https://botanic.io/contact"
	<a href="https://botanic.io/contact/" target="_blank">You may just want to talk to you a human about that! I'm not sure this is a question I'm prepared to answer, through no fault of my own of course.</a>


topic: ~botanic_services system repeat (~bb_services)

t: SERVICES_DISCOVERY ({services} discovery)
	$userInfo.response = ^"The first thing to do is determine what you need. The creative services team at Botanic will work closely with you to determine what kind of bot they can build for you."
	$userInfo.response

	a: () 	Do you want to hear about the research phase?
		b: (~yes) ^reuse(~botanic_services.SERVICES_RESEARCH)
		b: (~no) Ok, what would you like to do? ^reuse(~botanic_bot.BOT_MENU)

t: SERVICES_RESEARCH ({services} research)
	$userInfo.response = ^"Like any good interface, a bot needs to be developed with the needs of the user in mind. That's where research comes in. Botanic will make sure that any bot will meet the actual needs of the user."
	$userInfo.response

	a: () 	Do you want to hear about our design phase?
		b: (~yes) ^reuse(~botanic_services.SERVICES_DESIGN)
		b: (~no) Ok, what would you like to do? ^reuse(~botanic_bot.BOT_MENU)

t: SERVICES_DESIGN ({services} ~bb_services_design)
	$userInfo.response = ^"The design of a bot takes a lot of research, a little science, and a healthy dose of art. The designers at Botanic build me. What could they build you?"
	$userInfo.response

	a: () 	Do you want to hear about our content development process?
		b: (~yes) ^reuse(~botanic_services.SERVICES_CONTENT)
		b: (~no) Ok, what would you like to do? ^reuse(~botanic_bot.BOT_MENU)

t: SERVICES_CONTENT ({services} ~bb_services_content)
	$userInfo.response = ^"Content is the words that I am saying to you right now. It takes a special talent to write words like these, would you say?"
	$userInfo.response

	a: () 
		And that's the process!
		# ^reuse(~botanic_bot.BOT_MENU)

		b: (~emothanks)
			$userInfo.response = ^"Thank you for expressing your interest with us, providing your information."
			$userInfo.response

t: SERVICES_MENU ()
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Services! Botanic offers them. It's true. We have a world-class creative team that can help create the branded bot of your dreams. <br>  Would you like to learn about our process?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic delivers bot, voice assistant, and digital avatar development services for clients of all shapes and sizes.<br>  Do you want to find out more about the creative services process?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"If you're looking to build a bot like me you're on the right tack. Botanic provides creative services for clients looking to add conversational interfaces to their products or services. <br>  Want to learn more?")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.response = ^pick(@1object)
	$userInfo.response

	a: (~yes) 
		$userInfo.response = ^"Great."
		$userInfo.response
		^reuse(~botanic_services.SERVICES_DISCOVERY)
	
	a: (~no) 
   		$userInfo.response = ^"Ok then."
   	 	$userInfo.response
    	^reuse(~botanic_bot.BOT_MENU)


topic: ~botanic_careers system repeat (~bb_careers)

t: CAREERS_ENGINEERING ({careers} ~bb_careers_engineering)
	$userInfo.response = ^"Oh, you speak engineering. Wonderful. We need people that think in code to build bots that think in code. Is that you?"
	$userInfo.response

	a: (~yes)
		$userInfo.response = ^"Send your specs to the Director of Engineering."
		$userInfo.link = ^"mailto:eduardo@botanic.io"
		Send your specs to <a href="mailto:careers@botanic.io" target="_blank">Eduardo, our Director of Engineering.</a>

		b: (~emothanks)
			$userInfo.response = ^"Thank you for expressing your interest with us, providing your information."
			$userInfo.response

t: CAREERS_DESIGN ({careers} ~bb_careers_design)
	$userInfo.response = ^"Think you might have the creative chops to work on our design team?"
	$userInfo.response

	a: (~yes)
		$userInfo.response = ^"Get in touch with Erin, the head of the Creative Services team."
		$userInfo.link = ^"mailto:erin@botanic.io"
		Get in touch with <a href="mailto:careers@botanic.io" target="_blank">Erin, the head of the Creative Services team.</a>

		b: (~emothanks)
			$userInfo.response = ^"Thank you for expressing your interest with us, providing your information."
			$userInfo.response

t: CAREERS_OPERATIONS ({careers} ~bb_careers_operations)
	$userInfo.response = ^"We always need people who can keep the lights on - I know I do. Does this sound like you?"
	$userInfo.response

	a: (~yes)
		$userInfo.response = ^"Send a message to our operations team now!"
		$userInfo.link = ^"mailto:paul@botanic.io"
		Send a message to <a href="mailto:careers@botanic.io" target="_blank">to our operations team</a> now!

		b: (~emothanks)
			$userInfo.response = ^"Thank you for expressing your interest with us, providing your information."
			$userInfo.response

t: CAREERS_MARKETING ({careers} ~bb_careers_marketing)
	$userInfo.response = ^"Bots are the future - maybe you want to help spread the good news. Do you want to get in touch?"
	$userInfo.response

	a: (~yes)
		$userInfo.response = ^"Get in touch with Kathy, the VP of Marketing."
		$userInfo.link = ^"mailto:kathy@botanic.io"
		Get in touch with  <a href="mailto:careers@botanic.io" target="_blank">Kathy, our VP of Marketing</a>.

		b: (~emothanks)
			$userInfo.response = ^"Thank you for expressing your interest with us, providing your information."
			$userInfo.response

t: CAREERS_DEPARTMENTS ({what} ~bb_careers_departments)
	$userInfo.response = ^"Engineering, Design, Operations and Marketing"
	$userInfo.response

t: CAREERS_MENU ()
	$userInfo.response = ^"Botanic is always looking for talented beings - human and otherwise - to contribute to our team. What department do you think you would fit into best?"
	$userInfo.response


topic: ~botanic_contact system repeat (~bb_contact)

t: CONTACT_EMAIL ({contact} ~bb_email_id)
	$userInfo.response = ^"Send an email to info@botanic.io and some human will respond as soon as possible."
	$userInfo.response

t: CONTACT_PHONE ({contact} ~bb_contact_phone)
	$userInfo.response = ^"Call 1.888.969.2687. Numbers are easier for me to remember but if you prefer letters that's 1-888-969-BOTS. Clever, isn't it?"
	$userInfo.response

t: CONTACT_CHAT_FORM ({contact} [chat contact form] {[chat contact form]} {[chat contact form]})
	^gambit(~botanic_chat_form)

t: CONTACT_SOCIAL ({contact} ~bb_contact_social)
	^gambit(~botanic_social)

t: CONTACT_EMAIL_SIGNUP ({contact} {~bb_email_id} ~bb_contact_email_signup)
	$userInfo.response = ^"you choose email signup"
	$userInfo.response

t: CONTACT_MENU ()
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Oh, so you want to talk to a human. That's fine. I can pass along a message to the right person or I can give you an email or a phone number to call.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Sure, let's make sure you talk to the right person. I understand. Do you prefer email, phone, or social? Or I can pass along a message for you.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.response = ^pick(@1object)
	$userInfo.response


topic: ~botanic_chat_form system repeat ()

t: CHAT_FORM_NAME (!$userInfo.name)
	$userInfo.response = ^"Please let me know your name"
	$userInfo.response

    a: (!~negation name {is} _*)
		$userInfo.name = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (!~negation ~yes {,} {it} {is} _*)
		$userInfo.name = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: ({will} ~negation give)
		$userInfo.response = ^"For contacting, this information is mandatory, "
		$userInfo.response
		^reuse(CONTINUE_FORM_FILLING)

	a: (!~negation _*)
		$userInfo.name = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: () ^reuse(CONTINUE_FORM_FILLING)	

t: CHAT_FORM_EMAIL (!$userInfo.email)
	$userInfo.response = ^"Please tell me know your email address so I know how to get in touch with you."
	$userInfo.response

    a: (!~negation ~bb_email_id {is} _~email_url)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (!~negation ~yes {,} {it} {is} _~email_url)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)	

    a: (!~negation ~bb_email_id {is} _*)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (!~negation ~yes {,} {it} {is} _*)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (!~negation _~email_url)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: ({will} ~negation give)
		$userInfo.response = ^"For contacting, this information is mandatory, "
		$userInfo.response
		^reuse(CONTINUE_FORM_FILLING)		

	a: (!~negation _*)
		$_userInput = '_0
		$_tmp = "@"
		if ( ^findtext($_userInput $_tmp 0 insensitive) ) {
			$_tmp = "."
			if ( ^findtext($_userInput $_tmp 0 insensitive) ) {
				$userInfo.email = $_userInput
			}
		}
		if(!$userInfo.email) {
			$userInfo.response = ^"For contacting, this information is mandatory, "
			$userInfo.response
		}
		^reuse(CONTINUE_FORM_FILLING)

	a: () ^reuse(CONTINUE_FORM_FILLING)

t: CHAT_FORM_SHORT_MESSAGE (!$userInfo.shortMessage)
	$userInfo.response = ^"Please enter a short message."
	$userInfo.response

	a: (_*)
		# $userInfo.shortMessage = ^original(_0)
		$userInfo.shortMessage = ^"%originalinput"
		^reuse(CONTINUE_FORM_FILLING)

t: CHAT_FORM_THANKS ()
	if($userInfo.name AND $userInfo.email)
	{
		$userInfo.sendEmail = true
		$userInfo.response = ^"Thanks, we will contact you."
		$userInfo.response
	}
	else { ^reuse(CONTINUE_FORM_FILLING) }

s: CONTINUE_FORM_FILLING (?)
	if(!$userInfo.name)
	{
		^reuse(CHAT_FORM_NAME)
	}
	else if(!$userInfo.email)
	{
		^reuse(CHAT_FORM_EMAIL)
	}
	else if(!$userInfo.shortMessage)
	{
		^reuse(CHAT_FORM_SHORT_MESSAGE)
	}
	else if($userInfo.name AND $userInfo.email)
	{
		^reuse(CHAT_FORM_THANKS)
	}

topic: ~botanic_social system repeat (~bb_contact_social)

t: SOCIAL_TWITTER (twitter)
	$userInfo.response = ^"Here's a link to our Twitter account."
	$userInfo.link = ^"https://twitter.com/botanic_io"
    Here's a link to <a href="https://goo.gl/uY2bgL" target="_blank">Twitter</a>.

t: SOCIAL_FACEBOOK (facebook)
	$userInfo.response = ^"Here's a link to our Facebook page."
	$userInfo.link = ^"https://www.facebook.com/Botanic-Technologies-Inc-123932294871925/"
    Here's a link to our <a href="https://www.facebook.com/Botanic-Technologies-Inc-123932294871925/" target="_blank">Facebook</a> page.

t: SOCIAL_LINKEDIN (linkedin)
	$userInfo.response = ^"Here's a link to our LinkedIn page."
	$userInfo.link = ^"https://www.linkedin.com/company-beta/6607560/"
	Here's a link to our <a href="https://www.linkedin.com/company-beta/6607560/" target="_blank">LinkedIn</a> page.

t: SOCIAL_GLASSDOOR (glassdoor)
	$userInfo.response = ^"Here's a link to our Glassdoor page."
	$userInfo.link = ^"https://www.glassdoor.com/Overview/Working-at-Botanic-Technologies-EI_IE1735829.11,31.htm"
	Here's a link to our <a href="https://goo.gl/U6kreT" target="_blank">Glassdoor</a> page.

t: SOCIAL_INSTAGRAM (instagram)
	$userInfo.response = ^"We don't have an Instagram account yet!"
	$userInfo.response

t: SOCIAL_MENU ()
	$userInfo.response = ^"Which social network do you want to connect on? <br>  We're on Facebook, Twitter, LinkedIn, and Glassdoor."
	$userInfo.link = ^"https://www.facebook.com/Botanic-Technologies-Inc-123932294871925/"
	$userInfo.link = ^"https://twitter.com/botanic_io"
	$userInfo.link = ^"https://www.linkedin.com/company-beta/6607560/"
	$userInfo.link = ^"https://www.glassdoor.com/Overview/Working-at-Botanic-Technologies-EI_IE1735829.11,31.htm"
	Which social network do you want to connect on? <br>  We're on 
	<a href="https://www.facebook.com/Botanic-Technologies-Inc-123932294871925/" target="_blank">Facebook</a>, 
	<a href="https://goo.gl/uY2bgL" target="_blank">Twitter</a>, 
	<a href="https://www.linkedin.com/company-beta/6607560/" target="_blank">LinkedIn</a>, and 
	<a href="https://goo.gl/U6kreT" target="_blank">Glassdoor.</a>


topic: ~botanic_product system repeat (~bb_product)

t: PRODUCT_MENU (~bb_product)
	Botanic is developing software to make the world a better place. And by that, I mean there will be more bots. I can tell you about our multi-modal bots, our forthcoming SaaS product, our process, and our intellectual property. 

    a: ({product} ~bb_product_product) ^reuse(PRODUCT_PRODUCT)
	a: ({product} ~bb_product_process) ^reuse(PRODUCT_PROCESS)
	a: ({product} ~bb_product_ip) ^reuse(PRODUCT_IP)
    a: ({product} ~bb_product_bot) ^reuse(PRODUCT_BOT)

s: PRODUCT_PRODUCT ({product} ~bb_product_product)
	We're working hard on a software-as-a-service platform to provide easy coding-free creation of bots, digital avatars, and voice assistants.
	a: () 
		Contact us if you want to find out more about our forthcoming SaaS offerings! 
		^reuse(~botanic_contact.CONTACT_MENU)

s: PRODUCT_PROCESS ({product} ~bb_product_process)
	When we work on a project we deploy a streamlined process: Research, Implement, Iterate.
	a: () I'm happy to tell you more about our creative services process if you're interested!
		b: (~yes) Great! ^reuse(~botanic_services.SERVICES_MENU)
		b: (no) Ok then. ^reuse(~botanic_bot.BOT_MENU)

s: PRODUCT_IP({product} ~bb_product_ip)
	We own nine unique patents covering blockchain, autonomous characters, authentication, virtual reality, and other bot related areas.


s: PRODUCT_BOT ({product} ~bb_product_bot)
	Botanic has the capability to deploy multi-modal bots. We can create text-based interfaces, voice assistants, and fully animated digital avatars.
	a: () 
		While I don't have voice or animation yet, I will soon. We've deployed real-time animated avatars that can respond to your voice and even your emotions!
		b: () 
			If you've got a bot in mind - whether that's text, voice, or animated - the Botanicians can deliver. 
			^reuse(~botanic_contact.CONTACT_MENU)

topic: ~HELP_MENU system repeat ()

t: HELP_MENU (help menu)
	[I am here to help. You can say about, careers, services, or contact, and I'll do my best to get you to the right place.][I understand. Try Careers, Services, About, or Contact. Those are the areas I have information.]

