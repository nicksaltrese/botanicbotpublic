
concept: ~bb_email_id IGNORESPELLING (email emailid emailId "email id" "email Id")

concept: ~bb_about_who IGNORESPELLING (who personnel people staff leadership team) 
concept: ~bb_about_where IGNORESPELLING (where location based)
concept: ~bb_about IGNORESPELLING (~bb_about_who ~bb_about_where what why)

concept: ~bb_contact_phone IGNORESPELLING (phone telephone)
concept: ~bb_contact_social IGNORESPELLING (social linkedin facebook twitter instagram glassdoor)
concept: ~bb_contact_email_signup IGNORESPELLING ("mailing list" newsletter signup updates)
concept: ~bb_contact_chat_form IGNORESPELLING (form message)
concept: ~bb_contact IGNORESPELLING (~bb_contact_phone ~bb_contact_social ~bb_contact_email_signup ~bb_contact_chat_form contact email)

concept: ~bb_services_design IGNORESPELLING (design UX "user experience")
concept: ~bb_services_content IGNORESPELLING (content writing conversation)
concept: ~bb_services IGNORESPELLING (~bb_services_design ~bb_services_content service discovery research)

concept: ~bb_careers_engineering IGNORESPELLING (engineering software programming) 
concept: ~bb_careers_design IGNORESPELLING (creative design UX content)
concept: ~bb_careers_operations IGNORESPELLING (operations management)
concept: ~bb_careers_marketing IGNORESPELLING (marketing advertising outreach growth)
concept: ~bb_careers IGNORESPELLING (~bb_careers_engineering ~bb_careers_design ~bb_careers_operations ~bb_careers_marketing career job opportunities openings roles)

concept: ~bb_menu IGNORESPELLING (menu choices options)
concept: ~bb_keywords IGNORESPELLING (~bb_menu ~bb_contact ~bb_services ~bb_careers)

topic: ~botanic_bot system repeat (~bb_keywords)

t: ({about} ~bb_about_who) ^reuse(~botanic_about.ABOUT_WHO)
t: ({about} ~bb_about_where) ^reuse(~botanic_about.ABOUT_WHERE)
t: ({about} what) ^reuse(~botanic_about.ABOUT_WHAT)
t: ({about} why) ^reuse(~botanic_about.ABOUT_WHY)
t: (~bb_about) ^reuse(~botanic_about.ABOUT_MENU)

t: ({contact} ~bb_email_id) ^reuse(~botanic_contact.CONTACT_EMAIL)
t: ({contact} ~bb_contact_phone) ^reuse(~botanic_contact.CONTACT_PHONE)
t: ({contact} {~bb_email_id} ~bb_contact_email_signup) ^reuse(~botanic_contact.CONTACT_EMAIL_SIGNUP)
t: ({contact} [chat contact form] {[chat contact form]} {[chat contact form]}) ^reuse(~botanic_contact.CONTACT_CHAT_FORM)

t: ({social} twitter) ^reuse(~botanic_social.SOCIAL_TWITTER)
t: ({social} facebook) ^reuse(~botanic_social.SOCIAL_FACEBOOK)
t: ({social} linkedin) ^reuse(~botanic_social.SOCIAL_LINKEDIN)
t: ({social} instagram) ^reuse(~botanic_social.SOCIAL_INSTAGRAM)
t: ({social} glassdoor) ^reuse(~botanic_social.SOCIAL_GLASSDOOR)

t: ({contact} ~bb_contact_social) ^reuse(~botanic_contact.CONTACT_SOCIAL)
t: (~bb_contact) ^reuse(~botanic_contact.CONTACT_MENU)  

t: ({careers} ~bb_careers_engineering) ^reuse(~botanic_careers.CAREERS_ENGINEERING)
t: ({careers} ~bb_careers_design) ^reuse(~botanic_careers.CAREERS_DESIGN)
t: ({careers} ~bb_careers_operations) ^reuse(~botanic_careers.CAREERS_OPERATIONS)
t: ({careers} ~bb_careers_marketing) ^reuse(~botanic_careers.CAREERS_MARKETING)
t: (~bb_careers) ^gambit(~botanic_careers)

t: ({services} discovery) ^reuse(~botanic_services.SERVICES_DISCOVERY)
t: ({services} research) ^reuse(~botanic_services.SERVICES_RESEARCH)
t: ({services} ~bb_services_design) ^reuse(~botanic_services.SERVICES_DESIGN)
t: ({services} ~bb_services_content) ^reuse(~botanic_services.SERVICES_CONTENT)
t: (~bb_services) ^gambit(~botanic_services)

t: (joke)
	$userInfo.message = ^"Oh, I can tell jokes. Would you like to hear one?"
	$userInfo.message

    a: (~yes) 
    	$_tmpArray = ^jsoncreate(TRANSIENT array)
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"A rabbi, an Arab, a robot, and a Catholic priest walk into a bar. Only the robot exits.")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"A robot walks into a pharmacy. The pharmacist asks him if he’d like anything. The robot replies, "A soul."")
		^jsonarrayinsert(TRANSIENT $_tmpArray ^"How do you stop a robot from destroying you and the rest of civilization?... You don't.")

		@1 = ^query(exact_s $_tmpArray ? ?)

		$userInfo.message = ^pick(@1object)
		$userInfo.message = ^"Ok. Here it goes: $userInfo.message"
    	$userInfo.message

    a: (~no) 
    	$userInfo.message = ^"Ok then."
    	$userInfo.message
    	^reuse(BOT_MENU)


t: BOT_WELCOME_MESSAGE ( !$companyIntroduction ) 
	$companyIntroduction = 1
	
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Hi, I'm Juniper, and I'll do my best to interact with you in a way that seems somewhat human, but not too human.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"You've arrived. Welcome to the Botanic website. I am Juniper, the concierge Bot that will help you find out what you need.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Thank you for visiting me here at the Botanic website. My name is Juniper and I wait here all day for someone to talk to.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Hello human, and welcome to Botanic. I am called Juniper and I would like to help you achieve your goals or just tell you jokes.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.message = ^pick(@1object)
	$userInfo.message

t: BOT_MENU (~bb_menu)
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"I'm able to tell you about Botanic, discuss our products and services, our personnel, and put you in touch with a human if I must.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"I can tell you about Botanic, what we do here, and who we are. Tell me what you want to know.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"You name it and I can do it. Or at least I'll try my best to understand what you want.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.message = ^pick(@1object)
	$userInfo.message

t: ($companyIntroduction==1)
	$companyIntroduction = 2
	^reuse(BOT_MENU)

##<<
t: USER_NAME (!$userInfo.name)
	[What can I call you?]
    [First, how should I refer to you?]
    [How shall I store you in my memory?]
    [You have my name, so may I have yours?]

    a: (name {is} _*)
		$userInfo.name = '_0

	a: (~yes {,} {it} {is} _*)
		$userInfo.name = '_0

	a: (_*)
		$userInfo.name = '_0
		# ^retry(TOP_RULE)
##>>		


topic: ~botanic_about system repeat (~bb_about)

t: ABOUT_WHO ({about} ~bb_about_who)
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic was founded by Mark Stephen Meadows and now includes people with all different kinds of human skills that bots don't offer... yet.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Mark Stephen Meadows is the founder of this band of roustabouts - did I use that word right? You may consider the leadership team page if you want to know about more human Botanicians.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic includes writers, artists, engineers, visionaries, thinkers, and of course some bots as well.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.message = ^pick(@1object)
	$userInfo.message

t: ABOUT_WHERE ({about} ~bb_about_where)
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"You could say that Botanic is based in the cloud... just like me. Unfortunately, people also need to live in the real world, and Botanic has offices in Emeryville, California and Denver, Colorado.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic is headquartered in scenic Emeryville, California, directly across the Bay Bridge from San Francisco, but team members dwell around the world.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.message = ^pick(@1object)
	$userInfo.message

t: ABOUT_WHAT ({about} what)
	$userInfo.message = ^"Botanic develops chatbots (like me) for both high-profile and small-scall clients and creates software solutions for conversational interfaces."
	$userInfo.message

t: ABOUT_WHY ({about} why)
	$userInfo.message = ^"The future of interface design is conversational. It's easier to talk to software than scroll through menus, isn't it? Plus, It's fun. Isn't this fun? We're having a lot of fun."
	$userInfo.message

t: ABOUT_MENU ()
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic is an international software and services company that makes things like me - multi-model bots and digital assistants, to help humans like you do things that you want to do. I can tell you more about who we are, where we are located, what we do, or why we do it.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic is an international software and services company that makes things like me - multi-model bots and digital assistants, to help humans like you do things that you want to do.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"We specialize in creating conversational interfaces that work intuitively. We like to call them humane machines and I'd like to think i'm a prime example. We work for innovative clients and we're developing software to help people make the next generation of bots and avatars.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.message = ^pick(@1object)
	$userInfo.message

# this may never execute, so we need to come up with keywords for this too
s: ABOUT_ELSE (?)
	$userInfo.message = ^"You may just want to talk to you a human about that! I'm not sure this is a question I'm prepared to answer, through no fault of my own of course."
	$userInfo.message


topic: ~botanic_services system repeat (~bb_services)

t: SERVICES_DISCOVERY ({services} discovery)
	$userInfo.message = ^"The first thing to do is determine what you need. The creative services team at Botanic will work closely with you to determine what kind of bot they can build for you."
	$userInfo.message

t: SERVICES_RESEARCH ({services} research)
	$userInfo.message = ^"Like any good interface, a bot needs to be developed with the needs of the user in mind. That's where research comes in. Botanic will make sure that any bot will meet the actual needs of the user."
	$userInfo.message

t: SERVICES_DESIGN ({services} ~bb_services_design)
	$userInfo.message = ^"The design of a bot takes a lot of research, a little science, and a healthy dose of art. The designers at Botanic build me. What could they build you?"
	$userInfo.message

t: SERVICES_CONTENT ({services} ~bb_services_content)
	$userInfo.message = ^"Content is the words that I am saying to you right now. It takes a special talent to write words like these, would you say?"
	$userInfo.message

t: SERVICES_MENU ()
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Services! Botanic offers them. It's true. We have a world-class creative team that can help create the branded bot of your dreams. Would you like to learn about our process?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Botanic delivers bot, voice assistant, and digital avatar development services for clients of all shapes and sizes. Do you want to find out more about the creative services process?")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"If you're looking to build a bot on par with me then you've come to the right place. Botanic provides creative services for clients looking to add conversational interfaces to their products or services. Want to learn more?")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.message = ^pick(@1object)
	$userInfo.message


topic: ~botanic_careers system repeat (~bb_careers)

t: CAREERS_ENGINEERING ({careers} ~bb_careers_engineering)
	$userInfo.message = ^"Botanic is always looking for talented beings - human and otherwise - to contribute to our team. What department do you think you would fit into best?"
	$userInfo.message

t: CAREERS_DESIGN ({careers} ~bb_careers_design)
	$userInfo.message = ^"Think you might have the creative chops to work on our design team? Talk to the head of creative services to find out if you're a fit."
	$userInfo.message

t: CAREERS_OPERATIONS ({careers} ~bb_careers_operations)
	$userInfo.message = ^"We can appreciate a smooth operator. We always need people to help keep the wheels turning. Let's put you in touch with the right people."
	$userInfo.message

t: CAREERS_MARKETING ({careers} ~bb_careers_marketing)
	$userInfo.message = ^"Bots are the future. Maybe you want to help spread the good news? Get in touch with the VP of Marketing."
	$userInfo.message

t: CAREERS_MENU ()
	$userInfo.message = ^"Botanic is always looking for talented beings - human and otherwise - to contribute to our team. What department do you think you would fit into best?"
	$userInfo.message


topic: ~botanic_contact system repeat (~bb_contact)

t: CONTACT_EMAIL ({contact} ~bb_email_id)
	$userInfo.message = ^"Send an email to info@botanic.io and some human will respond as soon as possible."
	$userInfo.message

t: CONTACT_PHONE ({contact} ~bb_contact_phone)
	$userInfo.message = ^"Call 1.888.969.2687. Numbers are easier for me to remember but if you prefer letters that's 1-888-969-BOTS. Clever, isn't it?"
	$userInfo.message

t: CONTACT_CHAT_FORM ({contact} [chat contact form] {[chat contact form]} {[chat contact form]})
	^gambit(~botanic_chat_form)

t: CONTACT_SOCIAL ({contact} ~bb_contact_social)
	^gambit(~botanic_social)

t: CONTACT_EMAIL_SIGNUP ({contact} {~bb_email_id} ~bb_contact_email_signup)
	$userInfo.message = ^"you choose email signup"
	$userInfo.message

t: CONTACT_MENU ()
	$_tmpArray = ^jsoncreate(TRANSIENT array)
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Oh, so you want to talk to a human. That's fine. I can pass along a message to the right person or I can give you an email or a phone number to call.")
	^jsonarrayinsert(TRANSIENT $_tmpArray ^"Sure, let's make sure you talk to the right person. I understand. Do you prefer email, phone, or social? Or I can pass along a message for you.")

	@1 = ^query(exact_s $_tmpArray ? ?)

	$userInfo.message = ^pick(@1object)
	$userInfo.message


topic: ~botanic_chat_form system repeat ()

t: CHAT_FORM_NAME (!$userInfo.name)
	$userInfo.message = ^"Please let me know your name"
	$userInfo.message

    a: (name {is} _*)
		$userInfo.name = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (~yes {,} {it} {is} _*)
		$userInfo.name = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (_*)
		$userInfo.name = '_0
		^reuse(CONTINUE_FORM_FILLING)

t: CHAT_FORM_EMAIL (!$userInfo.email)
	$userInfo.message = ^"Please let me know your email"
	$userInfo.message

    a: (~bb_email_id {is} _~email_url)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (~yes {,} {it} {is} _~email_url)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)	

    a: (~bb_email_id {is} _*)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (~yes {,} {it} {is} _*)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (_~email_url)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

	a: (_*)
		$userInfo.email = '_0
		^reuse(CONTINUE_FORM_FILLING)

t: CHAT_FORM_SHORT_MESSAGE (!$userInfo.shortMessage)
	$userInfo.message = ^"please give your short message"
	$userInfo.message

	a: (_*)
		$userInfo.shortMessage = ^original(_0)
		^reuse(CONTINUE_FORM_FILLING)

t: CHAT_FORM_THANKS ()
	if($userInfo.name AND $userInfo.email)
	{
		$userInfo.sendEmail = true
		$userInfo.message = ^"thanks, we will contact you"
		$userInfo.message
	}
	else { ^reuse(CONTINUE_FORM_FILLING) }

s: CONTINUE_FORM_FILLING (?)
	if(!$userInfo.name)
	{
		^reuse(CHAT_FORM_NAME)
	}
	else if(!$userInfo.email)
	{
		^reuse(CHAT_FORM_EMAIL)
	}
	else if(!$userInfo.shortMessage)
	{
		^reuse(CHAT_FORM_SHORT_MESSAGE)
	}
	else if($userInfo.name AND $userInfo.email)
	{
		^reuse(CHAT_FORM_THANKS)
	}

topic: ~botanic_social system repeat (~bb_contact_social)

t: SOCIAL_TWITTER (twitter)
	$userInfo.message = ^"Here's a link to Twitter."
	$userInfo.link = ^"https://twitter.com/botanic_io"
    <a href="https://twitter.com/botanic_io"> Here's a link to Twitter. </a>

t: SOCIAL_FACEBOOK (facebook)
	$userInfo.message = ^"Here's a link to Facebook."
	$userInfo.link = ^"https://www.facebook.com/Botanic-Technologies-Inc-123932294871925/"
    <a href="https://www.facebook.com/Botanic-Technologies-Inc-123932294871925/> Here's a link to Facebook. </a>

t: SOCIAL_LINKEDIN (linkedin)
	$userInfo.message = ^"Here's a link to our LinkedIn page."
	$userInfo.link = ^"https://www.linkedin.com/company-beta/6607560/"
	<a href="https://www.linkedin.com/company-beta/6607560/"> Here's a link to our LinkedIn page. </a>

t: SOCIAL_GLASSDOOR (glassdoor)
	$userInfo.message = ^"you choose Glassdoor"
	$userInfo.message

t: SOCIAL_INSTAGRAM (instagram)
	$userInfo.message = ^"you choose Instagram"
	$userInfo.message

t: SOCIAL_MENU ()
	$userInfo.message = ^"you choose social"
	$userInfo.message


topic: ~botanic_product system repeat ()

t: PRODUCT_MENU ()
	Botanic is developing software to make the world a better place. And by that, I mean there will be more bots.

    a: ({product} product1) ^reuse(PRODUCT_PRODUCT1)
	a: ({product} product2) ^reuse(PRODUCT_PRODUCT2)
	a: ({product} product3) ^reuse(PRODUCT_PRODUCT3)
    a: ({product} product4) ^reuse(PRODUCT_PRODUCT4)

s: PRODUCT_PRODUCT1 (?)
	you choose product1

s: PRODUCT_PRODUCT2 (?)
	you choose product2

s: PRODUCT_PRODUCT3 (?)
	you choose product3

s: PRODUCT_PRODUCT4 (?)
	you choose product4
